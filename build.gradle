
plugins {
    id 'build-dashboard'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'com.palantir.git-version' version '0.12.3'
    id 'groovy'
    id 'idea'
    id 'jacoco'
    id 'java-library'
    id 'nebula.maven-publish' version '17.3.2'
    id 'nebula.project' version '7.0.9'
    id 'project-report'
}

apply plugin: 'nebula.facet'

defaultTasks 'clean', 'printVersion', 'classes', 'projectReport', 'buildDashboard', 'check', 'jar', 'publishToMavenLocal'

//noinspection GroovyUnusedAssignment
group = 'org.shimomoto'
//noinspection GroovyAssignabilityCheck
version gitVersion()
//noinspection GroovyUnusedAssignment
sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

facets {
    integrationTest {
        parentSourceSet = 'main'
        //noinspection GroovyAssignabilityCheck
        testTaskName = 'integrationTest'
    }
}

configurations {
    integrationTestImplementation {
        extendsFrom testImplementation
    }
    integrationTestRuntime {
        extendsFrom testRuntime
    }
}
repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.jetbrains:annotations:20.1.0'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'
    compileOnly 'org.jetbrains:annotations:20.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.16'

    implementation 'com.codepoetics:protonpack:1.16'
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.codehaus.groovy:groovy-all:2.5.14'
    testImplementation 'org.hamcrest:hamcrest-core:2.2'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation('com.athaydes:spock-reports:1.8.0') {
        transitive = false
    }

    testRuntime 'cglib:cglib-nodep:3.2.12'
    testRuntime 'org.objenesis:objenesis:2.6'

    integrationTestCompileOnly 'org.projectlombok:lombok:1.18.16'
    integrationTestCompileOnly 'org.jetbrains:annotations:20.1.0'
}

test {
    testLogging {
//        events 'passed', 'skipped', 'failed'
        events 'failed'
    }
    finalizedBy jacocoTestReport
}

integrationTest {
//    maxHeapSize = "4096m"
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
    finalizedBy jacocoTestReport
    outputs.upToDateWhen { false }
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['org/shimomoto/yakety/csv/config/*',])
        }))
    }
    dependsOn test
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }

    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }

    resolutionStrategy {
        componentSelection {
            all {
                if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
                    reject('Release candidate')
                }
            }
        }
    }
}
